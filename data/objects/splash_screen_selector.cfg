{
id: "splash_screen_selector",
ignore_collide: true,
zorder: "@include data/zorder.cfg:in_front_of_everything",
is_human: true,

editor_info: {
	category: "cat",
	},
	
vars: {
	currently_selected: null,
    current_index: 0,
    items: [],
	},
	
consts: {
	offset: [-18,0],
	},

properties: {
	item_chars: "def() filter(level.chars, value.consts.is_menu_item)",

    sorted_items_top_down: "def() sort(item_chars(), a.y < b.y)",

    default_select: "def() if(defaults.size > 0, defaults[0], item_chars()[0])
      where defaults = filter(item_chars(), value.default_selection)",

	select: "def(item) if(item, [
		set(xy, list_add(item.xy, consts.offset)),
		set(currently_selected, item),
		fire_event(item, 'selected'),
		if(currently_selected, [
			fire_event(currently_selected, 'deselected'),
		]),
		fire_event(item, 'activate'),
		if(level.cycle > 10, sound('MenuCursorMove.ogg')),
		],[
		fire_event('failed_select'),
		])",

    shuffle_down: "def()
      map(items, item, [shuffle_item('down', item, y_vals)])
      where y_vals = map(items, item, item.y)",

    shuffle_up: "def()
      map(items, item, [shuffle_item('up', item, y_vals)])
      where y_vals = map(items, item, item.y)",

    shuffle_item: "def(direction, item, y_vals)
      set(item.y, y_vals[if(direction = 'down', prev, next)])
      where next  = (idx + 1) % items.size
      where prev  = if((idx <= 0), items.size - 1, idx - 1)
      where idx   = index(items,item)",
    
	screen_scale_factor: "if(DOUBLE_SCALE, 1, 2)",
},

on_start_level: "[

	set(alpha,0),
	]",
	
on_ctrl_down: "
  [
    set(current_index, next),
    select(items[next])
  ]
  where next  = (current_index + 1) % items.size
  where prev  = if((current_index <= 0), items.size - 1, current_index - 1),
",
on_ctrl_up: "
  [
    set(current_index, prev),
    select(items[prev])
  ]
  where next  = (current_index + 1) % items.size
  where prev  = if((current_index <= 0), items.size - 1, current_index - 1),
",
on_ctrl_right: "fire_event('ctrl_down')",
on_ctrl_left: "fire_event('ctrl_up')",


on_ctrl_tongue: "fire_event('finish_menu')",
on_ctrl_attack: "fire_event('ctrl_tongue')",
on_ctrl_jump: "fire_event('ctrl_tongue')",

on_finish_menu: "if(cycle > 30, [
                swallow_event(),
                set(control_lock, []),
                screen_flash([255,255,255,0], [0,0,0,5], 52),
                schedule(50,
                  [set(control_lock, null),
                    switch(currently_selected.msg,
                    'CONTINUE', [load_game('instant', currently_selected.vars.save_slot_index)],
                    'NEW GAME', [spawn_player('cat', 0, 0, 1, []), teleport('world_1.cfg','instant')],
                    'EXIT'  , end_game() ,
                     )
                  ]
                  )
                ])
             ",

on_process:"
[
	if(13 in ctrl_keys or ' ' in ctrl_keys,
		fire_event('finish_menu')
	)
]",

on_create:"[set(items, sorted_items_top_down()),
            select(default_select()),
            set(current_index, index(sorted_items_top_down(), default_select()))]",

animation: {
	id: "normal",
	image: "characters/stand_in_selector.png",
	rect: [0,0,15,15],
},

}
